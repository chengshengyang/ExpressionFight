apply plugin: 'com.android.application'
apply plugin: 'com.mob.sdk'

android {
    compileSdkVersion 28
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.csy.fight"
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        flavorDimensions "releaseChannal"
    }

    signingConfigs {
        release {
            try {
                storeFile file('ExpressionFight.jks')
                storePassword KEYSTORE_PASSWORD
                keyAlias "expression"
                keyPassword KEY_PASSWORD
            } catch (Exception e) {
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            manifestPlaceholders = [DEBUG_FLAG: false]
            signingConfig signingConfigs.release
            proguardFiles 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false // 混淆开关
            shrinkResources false // 去除无效资源文件开关
            proguardFiles 'proguard-rules.pro' // 混淆规则文件
            manifestPlaceholders = [DEBUG_FLAG: true]
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (variant.buildType.name != "debug") {
                output.outputFileName = "${defaultConfig.versionName}." +
                        "release${releaseTime()}_${productFlavors.manifestPlaceholders.RELEASE_CHANNEL_VALUE}.apk"
            }
        }
    }

    // 多渠道打包配置
    productFlavors {
        expression {
            dimension "releaseChannal"
        }

        fight {
            dimension "releaseChannal"
        }
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [RELEASE_CHANNEL_VALUE: name]
    }

    // 移除lint检查的error
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        disable 'MissingTranslation', 'ExtraTranslation'
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }
}

static def releaseTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("GMT+8"))
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.android.support:support-v4:27.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    implementation 'com.github.bumptech.glide:glide:4.6.1'
    implementation 'com.github.chrisbanes:PhotoView:2.1.2'
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0'
}

MobSDK {
    appKey "2664c81edc6ba"
    appSecret "c1b2848c591bc8b395a574ce45089226"

    ShareSDK {
        //平台配置信息
        devInfo {
            Wechat {
                id 1
                sortId 1
                appKey "wx654c20629c47370c"
                appSecret "9109acb03d3f3c006a2736a6aae1d8da"
                bypassApproval true
                enable true
            }
            WechatMoments {
                id 2
                sortId 2
                appId "wx654c20629c47370c"
                appSecret "9109acb03d3f3c006a2736a6aae1d8da"
                bypassApproval false
                enable true
            }
            QQ {
                id 3
                sortId 3
                appId "1107388153"
                appKey "Lrm7ToJIGb9JvC4H"
                shareByAppClient true
                enable true
            }
            QZone {
                id 4
                sortId 4
                appId "1107388153"
                appKey "Lrm7ToJIGb9JvC4H"
                shareByAppClient true
                enable true
            }
        }
    }
}

